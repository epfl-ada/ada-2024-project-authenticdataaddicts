#This query returns the IMDB & Freebase ID's, as well as the titles and box office results for each movie

SELECT ?item ?freebase_id ?IMDb_ID ?title ?box_office WHERE {
  {
    SELECT DISTINCT ?item ?freebase_id ?IMDb_ID ?title WHERE {
      ?item p:P31/ps:P31/wdt:P279* wd:Q11424 .  # Filter for movies

      ?item wdt:P345 ?IMDb_ID .  # IMDB ID
      OPTIONAL { ?item wdt:P646 ?freebase_id . }  # Freebase ID (Optional)

      ?item wdt:P1476 ?title .  # Movie title
      ?item wdt:P577 ?pub_date .  # Release date
      FILTER(YEAR(?pub_date) <= 2012)
    }
  }
  OPTIONAL { ?item wdt:P2142 ?box_office .}  # Box office (Optional)
}
